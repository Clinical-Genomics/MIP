#!/usr/bin/env perl

use 5.022;
use Carp;
use charnames qw{ :full :short };
use English qw{ -no_match_vars };
use File::Basename qw{ dirname basename };
use File::Path qw{ remove_tree };
use File::Spec::Functions qw{ catfile catdir devnull };
use FindBin qw{ $Bin };
use Getopt::Long;
use open qw{ :encoding(UTF-8) :std };
use Params::Check qw{ check allow last_error};
use Test::More;
use utf8;
use warnings qw{ FATAL utf8 };

## Cpanm
use IPC::Cmd qw{ can_run run};
use Modern::Perl qw{ 2018 };
use Readonly;
use autodie qw{open close :all};

## MIPs lib/
use lib catdir( dirname($Bin), q{lib} );
use MIP::Constants qw{ $EMPTY_STR $NEWLINE $SPACE };
use MIP::Script::Utils qw{ help };

our $USAGE = build_usage( {} );

my $VERBOSE = 1;
our $VERSION = 1.03;

## Set paths
my $conda_path            = catdir( dirname($Bin), qw{ t data modules miniconda} );
my $cluster_constant_path = catdir( dirname($Bin), qw{ t data } );
my $config_file = catfile( dirname($Bin), qw{ templates mip_rd_rna_config.yaml } );
my $mip_path    = catfile( dirname($Bin), q{mip} );
my $dna_vcf_file = catfile( dirname($Bin),
    qw{ t data test_data 643594-miptest_sorted_md_brecal_comb_BOTH.bcf } );

### User Options
GetOptions(

    q{c|config_file:s} => \$config_file,

    ## Display help text
    q{h|help} => sub {
        done_testing();
        say {*STDOUT} $USAGE;
        exit;
    },

    ## Display version number
    q{v|version} => sub {
        done_testing();
        say {*STDOUT} $NEWLINE . basename($PROGRAM_NAME) . $SPACE . $VERSION . $NEWLINE;
        exit;
    },
    q{vb|verbose} => $VERBOSE,
  )
  or (
    done_testing(),
    help(
        {
            USAGE     => $USAGE,
            exit_code => 1,
        }
    )
  );

ok( can_run(q{mip}), q{Checking can run mip} );

## Test execution of mip
# Create array for cmd
my @base_cmds = (
    q{perl},
    $mip_path,
    qw{ analyse rd_rna 118-rnatest },
    q{--config},
    $config_file,
    q{--conda_path},
    $conda_path,
    q{--ccp},
    $cluster_constant_path,
    q{--ifd},
    catfile(
        $cluster_constant_path, qw{ 118-rnatest test_data 118-1-2A fastq=118-1-2A }
    ),
    q{--ifd},
    catfile(
        $cluster_constant_path, qw{ 118-rnatest test_data 118-2-1U fastq=118-2-1U }
    ),
    q{--ifd},
    catfile(
        $cluster_constant_path, qw{ 118-rnatest test_data 118-2-2U fastq=118-2-2U }
    ),
    qw{--dra --vb },
);

my @test_cases = ( $EMPTY_STR, q{--dna_vcf_file} );

TEST_CASE:
foreach my $test_case (@test_cases) {

    my @test_cmds = ( @base_cmds, $test_case );

    if ( $test_case eq q{--dna_vcf_file} ) {
        push @test_cmds, $dna_vcf_file;
    }

    my ( $success, $error_message, $full_buf, $stdout_buf, $stderr_buf ) = run(
        command => \@test_cmds,
        verbose => $VERBOSE
    );
    ok( $success, q{Executed mip} );

    my $qc_sample_info_file = catfile( $cluster_constant_path,
        qw{ 118-rnatest analysis 118-rnatest_qc_sample_info.yaml } );
    ok( -f $qc_sample_info_file, q{Checking for 118-rnatest_qc_sample_info.yaml} );
}

## Clean-up
remove_tree( catfile( $cluster_constant_path, qw{ 118-rnatest analysis } ) );

done_testing();

######################
####SubRoutines#######
######################

sub build_usage {

## Function : Build the USAGE instructions
## Returns  :
## Arguments: $program_name => Name of the script

    my ($arg_href) = @_;

    ## Default(s)
    my $program_name;

    my $tmpl = {
        program_name => {
            default     => basename($PROGRAM_NAME),
            store       => \$program_name,
            strict_type => 1,
        },
    };

    check( $tmpl, $arg_href, 1 ) or croak q{Could not parse arguments!};

    return <<"END_USAGE";
 $program_name [options]
    -c/--config_file YAML config file for analysis parameters (defaults to ../templates/mip_rd_rna_config.yaml")
    -t/--travis Travis parameters
    -vb/--verbose    Verbose
    -h/--help        Display this help message
    -v/--version     Display version
END_USAGE
}

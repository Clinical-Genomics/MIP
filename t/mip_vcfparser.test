#!/usr/bin/env perl

use 5.026;
use Carp;
use charnames qw{ :full :short };
use Cwd;
use English qw{ -no_match_vars };
use File::Basename qw{ dirname basename };
use File::Path qw{ remove_tree };
use File::Spec::Functions qw{ catdir catfile };
use FindBin qw{ $Bin };
use Getopt::Long;
use open qw{ :encoding(UTF-8) :std };
use Params::Check qw{ check allow last_error};
use Test::More;
use utf8;
use warnings qw{ FATAL utf8 };

## Cpanm
use IPC::Cmd qw{ can_run run};
use Modern::Perl qw{ 2018 };
use Readonly;
use autodie qw{open close :all};

## MIPs lib/
use lib catdir( dirname($Bin), q{lib} );
use MIP::Constants qw{ $NEWLINE $SPACE };
use MIP::Script::Utils qw{ help };

our $USAGE = build_usage( {} );

my $VERBOSE = 1;
our $VERSION = 1.01;

## Constants
Readonly my $SELECT_FEATURE_MATCHING_COLUMN => 3;

## Set paths
my $cluster_constant_path = catdir( dirname($Bin),          qw{ t data} );
my $conda_path            = catdir( $cluster_constant_path, qw{ modules miniconda} );
my $infile   = catfile( $cluster_constant_path, qw{ test_data test.vcf } );
my $mip_path = catfile( dirname($Bin),          q{mip} );
my $test_reference_path = catdir( $cluster_constant_path, q{references} );
my $pli_template_path =
  catfile( $test_reference_path, q{gnomad_pli_per_gene_-_r2.1.1-.txt} );
my $select_feature_file = catfile( dirname($Bin), qw{ templates gene_panels.bed } );
my $select_outfile      = catfile( $cluster_constant_path,
    qw{ 643594-miptest test_data 643594-miptest vcfparser_select.vcf } );

### User Options
GetOptions(

    ## Display help text
    q{h|help} => sub {
        done_testing();
        say {*STDOUT} $USAGE;
        exit;
    },

    ## Display version number
    q{v|version} => sub {
        done_testing();
        say {*STDOUT} $NEWLINE . basename($PROGRAM_NAME) . $SPACE . $VERSION . $NEWLINE;
        exit;
    },
    q{vb|verbose} => $VERBOSE,
  )
  or (
    done_testing(),
    help(
        {
            USAGE     => $USAGE,
            exit_code => 1,
        }
    )
  );

ok( can_run(q{mip}), q{Checking can run mip} );

## Test execution of mip
# Create array ref for cmd
my $cmds_ref = [
    q{perl},                         $mip_path,
    qw{ vcfparser},                  $infile,
    q{--pli},                        $pli_template_path,
    q{--parse_vep},                  q{--select_feature_file},
    $select_feature_file,            q{--select_feature_matching_column},
    $SELECT_FEATURE_MATCHING_COLUMN, q{--select_outfile},
    $select_outfile,
];

my ( $success, $error_message, $full_buf, $stdout_buf, $stderr_buf ) =
  run( command => $cmds_ref, verbose => $VERBOSE );
ok( $success, q{Executed mip vcfparser} );

ok( -f $select_outfile, q{Checking for select file} );

## Clean-up
unlink $select_outfile;
unlink catfile( cwd(), q{vcfparser.log} );

done_testing();

######################
####SubRoutines#######
######################

sub build_usage {

## Function : Build the USAGE instructions
## Returns  :
## Arguments: $program_name => Name of the script

    my ($arg_href) = @_;

    ## Default(s)
    my $program_name;

    my $tmpl = {
        program_name => {
            default     => basename($PROGRAM_NAME),
            store       => \$program_name,
            strict_type => 1,
        },
    };

    check( $tmpl, $arg_href, 1 ) or croak q{Could not parse arguments!};

    return <<"END_USAGE";
 $program_name [options]
    -vb/--verbose    Verbose
    -h/--help        Display this help message
    -v/--version     Display version
END_USAGE
}
